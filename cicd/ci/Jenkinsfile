pipeline {
    agent any

    environment {
        GITHUB_CREDENTIALS_ID = 'github'
        REPO_URL = 'https://github.com/Jenium-lab/exchangerate.git'
        SONAR_AUTH_TOKEN = credentials('sonarqube')
        DOCKER_CREDENTIALS_ID = 'dockerhub'
        IMAGE_NAME = 'srijan581/exchangerate'
        GRADLE_HOME = '/opt/gradle'
        PATH = "$GRADLE_HOME/bin:$PATH"
    }

    stages {
        stage('Checkout') {
            steps {
                script {
                    checkout([
                        $class: 'GitSCM',
                        branches: [[name: '*/main']],
                        userRemoteConfigs: [
                            [
                                url: REPO_URL,
                                credentialsId: GITHUB_CREDENTIALS_ID
                            ]
                        ]
                    ])
                }
            }
        }

        stage('Build') {
            steps {
                script {
                    sh './gradlew build -x test -x runSmokeTest'
                }
            }
        }

        stage('Unit Test') {
            steps {
                script {
                    sh './gradlew test'
                }
            }
        }

        stage('Smoke Test') {
            steps {
                script {
                    sh './gradlew runSmokeTest'
                }
            }
        }

        stage('Code Analysis') {
            steps {
                script {
                    withSonarQubeEnv('SonarQube') {
                        sh './gradlew sonar'
                    }

                    timeout(time: 1, unit: 'MINUTES') {
                        def qualityGate = waitForQualityGate(abortPipeline: true)
                        if (qualityGate.status != 'OK') {
                            error "SonarQube Quality Gate failed: ${qualityGate.status}"
                        }
                    }
                    echo "SonarQube Quality Gate passed."
                }
            }
        }

        stage('Build and Push docker image') {
            steps {
                script {
                    def commitHash = sh(script: 'git rev-parse --short HEAD', returnStdout: true).trim()
                    def imageTag = "${commitHash}"
                    def imageNameWithTag = "${IMAGE_NAME}:${imageTag}"

                    echo "Building and pushing image: ${imageNameWithTag}"

                    docker.withRegistry('https://index.docker.io/v1/', DOCKER_CREDENTIALS_ID) {
                        def image = docker.build(imageNameWithTag, ".")
                        image.push()
                    }

                    env.COMMIT_HASH = commitHash
                }
            }
        }

        stage('Deploy on Dev') {
            steps {
                script {
                    def containerName = "exchangerates-container"
                    def imageTag = env.COMMIT_HASH

                    // Stop and remove existing container if running
                    sh """
                        if [ \$(docker ps -q -f name=${containerName}) ]; then
                            echo "Stopping and removing existing container..."
                            docker stop ${containerName}
                            docker rm ${containerName}
                        fi
                    """

                    // Run the new container
                    sh """
                        echo "Running new container with image tag ${imageTag}..."
                        docker run -d --name ${containerName} -p 8090:8080 ${IMAGE_NAME}:${imageTag}
                    """
                }
            }
        }

        stage('Health check on Dev') {
            steps {
                script {
                    echo "Performing health check on Docker container..."
                    sleep 30

                    def healthUrl = "http://host.docker.internal:8090/actuator/health"
                    def response = sh(script: "curl -s ${healthUrl}", returnStdout: true).trim()
                    echo "Response from health check API: ${response}"

                    def jsonResponse = readJSON text: response
                    def status = jsonResponse.status

                    if (status == 'UP') {
                        echo 'Health check passed: Application is healthy.'
                    } else {
                        error 'Health check failed: Application is not healthy.'
                    }
                }
            }
        }

        stage('Update image tag') {
            steps {
                script {
                    def imageTag = env.COMMIT_HASH

                    sh """
                        git fetch origin
                        git checkout main
                        git pull origin main
                        sed -i 's/tag:.*/tag: ${imageTag}/' k8chart/values.yaml
                    """

                    withCredentials([usernamePassword(credentialsId: GITHUB_CREDENTIALS_ID, usernameVariable: 'GIT_USER', passwordVariable: 'GIT_PASS')]) {
                        sh """
                            git config user.email "kodedge-bot-ci@kodedge.com"
                            git config user.name "kodedge-bot-ci"
                            git add k8chart/values.yaml
                            git commit -m "Update image tag to version ${imageTag}"
                            git push https://${GIT_USER}:${GIT_PASS}@github.com/Jenium-lab/exchangerate.git main
                        """
                    }
                }
            }
        }
    }

    post {
        always {
            echo 'Cleaning up workspace after build'
            cleanWs()
        }

        success {
            echo 'Pipeline executed successfully!'
        }

        failure {
            echo 'Pipeline execution failed!'
        }
    }
}
